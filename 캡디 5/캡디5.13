libname A "C:\Users\mycom\Desktop\edu_sample_db\교육용(SAS)";
libname T "C:\Users\mycom\Desktop\table";


DATA T.M20;
SET A.NSC2_EDU_M20(KEEP=RN_INDI SICK_SYM1 MDCARE_STRT_DT);
SICK_SYM = .;
IF (SUBSTR(SICK_SYM1,1,3) IN ("E10" ,"E11", "E12", "E13", "E14")) THEN SICK_SYM="1";
	ELSE IF (SUBSTR(SICK_SYM1,1,3) IN ("I10","I11", "I12", "I13", "I14", "I15")) THEN SICK_SYM= "2";
	ELSE IF ((SUBSTR(SICK_SYM1,1,3) = "E78")) THEN SICK_SYM="4";
    ELSE SICK_SYM=0;
RUN;

PROC SQL;
CREATE TABLE T.M20_1 AS
SELECT DISTINCT RN_INDI, SUBSTR(MDCARE_STRT_DT,1,4) AS YEAR, SUM(DISTINCT(SICK_SYM)) AS SICK_SYM2
FROM T.M20
GROUP BY RN_INDI, YEAR
    ORDER BY RN_INDI, YEAR;
QUIT;
/* GROUP BY RN_INDI만 할지 YEAR도 같이할지 고민 */

PROC SQL;
CREATE TABLE T.BNC AS
SELECT RN_INDI, SEX, SIDO, CTRB_Q10
FROM  A.NSC2_EDU_BNC
GROUP BY RN_INDI;
QUIT;

PROC SQL;
CREATE TABLE T.JOIN AS
SELECT A.*, B.BTH_YYYY, C.SEX, C.SIDO, C.CTRB_Q10, C.STD_YYYY, D.SICK_SYM2
FROM A.NSC2_EDU_G1E AS A
LEFT JOIN A.NSC2_EDU_BND AS B ON A.RN_INDI=B.RN_INDI
LEFT JOIN A.NSC2_EDU_BNC AS C ON A.RN_INDI=C.RN_INDI AND A.EXMD_BZ_YYYY=C.STD_YYYY
LEFT JOIN T.M20_1 AS D ON A.RN_INDI=D.RN_INDI AND A.EXMD_BZ_YYYY=YEAR;
QUIT;

proc sql;
    create table T.SELECT_DATA as
    select RN_INDI, ROUND(mean(CTRB_Q10),1.) as mean_CTRB_Q10, CTRB_Q10, SIDO, SEX,  EXMD_BZ_YYYY,
Q_FHX_HTN, Q_FHX_DM, Q_DRK_FRQ_V0108, Q_DRK_AMT_V0108 ,Q_DRK_FRQ_V09N ,Q_DRK_AMT_V09N,
 Q_SMK_YN  ,Q_PA_FRQ ,G1E_BMI ,Q_PA_VD ,Q_PA_MD, Q_PA_WALK ,G1E_FBS ,G1E_TOT_CHOL,BTH_YYYY, SICK_SYM2
    from T.JOIN
    group by RN_INDI;
quit;

data T.FIX_MEAN;
    set T.SELECT_DATA;
    IF CTRB_Q10='.' THEN CTRB_Q10 = mean_CTRB_Q10; 
run;


DATA T.CATEGORI;
SET T.FIX_MEAN;
KEEP RN_INDI EXMD_BZ_YYYY SICK_SYM2 SICK_DM SICK_HTN SICK_HLD SICK_OX SEX AGE1 REGION INCOME DRINK BMI bloodsugar smoking 
Q_PA TOT_CHOL Q_FHX_HTN Q_FHX_DM ;
EXMD_YYYY = INPUT(EXMD_BZ_YYYY,4.);
YYYY=INPUT(BTH_YYYY,4.);
AGE=EXMD_YYYY-YYYY+1;
IF Q_SMK_YN='.' THEN DELETE;
IF G1E_BMI='.' THEN DELETE;
IF G1E_FBS='.' THEN DELETE;
IF G1E_TOT_CHOL='.' THEN DELETE;
IF CTRB_Q10='.' THEN DELETE;
IF (EXMD_BZ_YYYY="2010" OR EXMD_BZ_YYYY="2012") and Q_FHX_HTN=0 THEN Q_FHX_HTN=1;
	ELSE IF (EXMD_BZ_YYYY="2010" OR EXMD_BZ_YYYY="2012") and Q_FHX_HTN=1 THEN Q_FHX_HTN=2;
	ELSE IF Q_FHX_HTN='.' THEN Q_FHX_HTN=1;
IF (EXMD_BZ_YYYY="2010" OR EXMD_BZ_YYYY="2012") and Q_FHX_DM=0 THEN Q_FHX_DM=1;
	ELSE IF (EXMD_BZ_YYYY="2010" OR EXMD_BZ_YYYY="2012") and Q_FHX_DM=1 THEN Q_FHX_DM=2;
	ELSE IF Q_FHX_DM='.' THEN Q_FHX_DM=1;
/*나이*/
IF 0<=AGE<20 THEN AGE1="미포함";
   ELSE IF 20<=AGE<40 THEN AGE1='청년층';
   ELSE IF 40<=AGE<60 THEN AGE1='중장년충';
   ELSE IF 60<=AGE THEN AGE1='고령층';
/*흡연*/
IF Q_SMK_YN = 1 THEN smoking='1';
	ELSE smoking='2';
/*식전혈당*/
IF G1E_FBS <= 99 THEN bloodsugar = '1(good)';
	ELSE IF G1E_FBS >= 125 THEN bloodsugar = '3(bad)';
	ELSE bloodsugar = '2(bad)';
/*음주*/
IF Q_DRK_FRQ_V0108>1 OR Q_DRK_AMT_V0108>1 THEN DRINK='1';
   ELSE IF 0<=Q_DRK_FRQ_V0108<=1 OR 0<=Q_DRK_AMT_V0108<=1 THEN DRINK='0';
IF Q_DRK_FRQ_V09N>=2 OR Q_DRK_AMT_V09N>=3 THEN DRINK='1';
   ELSE IF 0<=Q_DRK_FRQ_V09N<2 OR 0<Q_DRK_AMT_V09N<3 THEN DRINK='0';
/*BMI*/
IF G1E_BMI<18.5 THEN BMI='저체중'; 
	ELSE IF G1E_BMI<23 THEN BMI='정상';
    	ELSE IF G1E_BMI<25 THEN BMI='과체중';
    		ELSE IF G1E_BMI<30 THEN BMI='비만';
	ELSE BMI='고도비만';
/*운동*/
IF (EXMD_BZ_YYYY="2006" OR EXMD_BZ_YYYY="2008") AND (Q_PA_FRQ=1 OR Q_PA_FRQ=2) THEN Q_PA=1;
   ELSE IF (EXMD_BZ_YYYY="2006" OR EXMD_BZ_YYYY="2008") AND 
            (Q_PA_FRQ=3 OR Q_PA_FRQ=4 OR Q_PA_FRQ=5) THEN Q_PA=2;
            ELSE IF (EXMD_BZ_YYYY="2010" OR EXMD_BZ_YYYY="2012") AND (0<Q_PA_VD+Q_PA_MD+Q_PA_WALK<3) THEN Q_PA=1;
               ELSE IF (EXMD_BZ_YYYY="2010" OR EXMD_BZ_YYYY="2012") AND (Q_PA_VD+Q_PA_MD+Q_PA_WALK>=3) THEN Q_PA=2;
                  ELSE Q_PA=0;
/*콜레스테롤*/
IF G1E_TOT_CHOL<200 THEN TOT_CHOL=0;
	ELSE IF 200<=G1E_TOT_CHOL<240 THEN TOT_CHOL=1;
	ELSE TOT_CHOL=2;
/*주상병*/
IF SICK_SYM2='.' THEN SICK_SYM2=0;
IF SICK_SYM2 = 1 OR SICK_SYM2 = 3 OR SICK_SYM2 = 5 OR SICK_SYM2 = 7 THEN SICK_DM = "당뇨";
	ELSE SICK_DM="X";
IF SICK_SYM2 = 2 OR SICK_SYM2 = 3 OR SICK_SYM2 = 6 OR SICK_SYM2 = 7 THEN SICK_HTN = "고혈압";
	ELSE SICK_HTN="X";
IF SICK_SYM2 = 4 OR SICK_SYM2 = 5 OR SICK_SYM2 = 6 OR SICK_SYM2 = 7 THEN SICK_HLD = "고지혈증";
	ELSE SICK_HLD="X";
IF SICK_SYM2>=1 THEN SICK_OX=1;
	ELSE SICK_OX=0;
/*지역*/
IF SIDO="11" THEN REGION="서울";
	ELSE REGION= "비수도권";
/*소득분위*/
IF CTRB_Q10 <=4 THEN INCOME = '하';
	ELSE IF CTRB_Q10 >=9 THEN INCOME = '상';
	ELSE INCOME = '중';
RUN;


DATA T.ANAL_DATA;
SET T.CATEGORI;
RETAIN RN_INDI EXMD_BZ_YYYY SEX AGE1 SICK_SYM2 REGION INCOME DRINK BMI BLOODSUGAR SMOKING Q_PA TOT_CHOL 
Q_FHX_HTN Q_FHX_DM SICK_DM SICK_HTN SICK_HLD SICK_OX;
RUN;


/* 전처리 후 분석*/
proc freq data=T.ANAL_DATA;
    tables SICK_SYM2;
RUN;
/*질병 O와 X는 7:3비율 정도 됨.*/

/*질병 유무에 따른 기초통계량*/
%macro frequency(sick, variable);
proc sort data=T.ANAL_DATA;
    by &sick; 
run;

proc freq data=T.ANAL_DATA ;
   BY &sick;
    tables &variable;
RUN; 
%mend;
%frequency(SICK_DM,SEX AGE1 SICK_SYM2 REGION INCOME DRINK BMI bloodsugar smoking Q_PA TOT_CHOL 
Q_FHX_HTN Q_FHX_DM);
%frequency(SICK_HTN,SEX AGE1 SICK_SYM2 REGION INCOME DRINK BMI bloodsugar smoking Q_PA TOT_CHOL 
Q_FHX_HTN Q_FHX_DM);
%frequency(SICK_HLD,SEX AGE1 SICK_SYM2 REGION INCOME DRINK BMI bloodsugar smoking Q_PA TOT_CHOL 
Q_FHX_HTN Q_FHX_DM);
%frequency(SICK_OX,SEX AGE1 SICK_SYM2 REGION INCOME DRINK BMI bloodsugar smoking Q_PA TOT_CHOL 
Q_FHX_HTN Q_FHX_DM );

/*ttest*/
/*ttest는 원데이터로 봐야할 것 같음 연속형이기 때문.*/
/*%macro ttest(sick,variable);*/
/*proc ttest data=T.ANAL_DATA;*/
/*class &sick;*/
/*var &variable;*/
/*run;*/
/*%mend;*/
/*%ttest(SICK_DM, variable);*/

/*카이제곱 검정*/
%macro chi(sick,variable);
proc freq data=T.ANAL_DATA;
table (&variable)*&sick/chisq nocol nopercent;
run;
%mend;
%chi(sick_dm, SEX AGE1 SICK_SYM2 REGION INCOME DRINK BMI bloodsugar smoking Q_PA TOT_CHOL 
Q_FHX_HTN Q_FHX_DM );
%chi(sick_htn, SEX AGE1 SICK_SYM2 REGION INCOME DRINK BMI bloodsugar smoking Q_PA TOT_CHOL 
Q_FHX_HTN Q_FHX_DM );
%chi(sick_hld, SEX AGE1 SICK_SYM2 REGION INCOME DRINK BMI bloodsugar smoking Q_PA TOT_CHOL 
Q_FHX_HTN Q_FHX_DM );
%chi(sick_OX, SEX AGE1 SICK_SYM2 REGION INCOME DRINK BMI bloodsugar smoking Q_PA TOT_CHOL 
Q_FHX_HTN Q_FHX_DM );

/*단순 로지스틱 회귀분석*/
/*%macro logistic(var,standard,sick, sick_index);*/
/*proc logistic data=T.ANAL_DATA;*/
/*   class &var(ref=&standard);*/
/*    model &sick(event=&sick_index)= &var;*/
/*run;*/
/*%mend;*/
/*당뇨*/
%macro logistic(var,standard);
proc logistic data=T.ANAL_DATA;
   class &var(ref=&standard);
    model sick_dm(event="당뇨")= &var;
run;
%mend;
%logistic(BMI, '정상' );
%logistic(INCOME,"중");

%macro logistic(var,standard);
proc logistic data=T.ANAL_DATA;
   class &var(ref=&standard);
    model sick_htn(event="고혈압")= &var;
run;
%mend;



/*다중 로지스틱 회귀분석*/
%macro multi_log(sick,sick_index);
proc logistic data=T.ANAL_DATA;
class sex(ref='1') REGION(ref='서울') AGE1(ref='청년층') BMI(ref='정상') TOT_CHOL(ref='1') Q_FHX_HTN(ref='1') Q_FHX_DM(ref='1')
         DRINK(ref='0') smoking(ref='1') Q_PA(ref='1') INCOME(ref='중') BLOODSUGAR(ref="1(good)");
model &sick(event=&sick_index) = sex REGION AGE1 BMI TOT_CHOL Q_FHX_HTN Q_FHX_DM DRINK smoking Q_PA INCOME BLOODSUGAR;
run;
%mend;

%multi_log(sick_dm, "당뇨");


